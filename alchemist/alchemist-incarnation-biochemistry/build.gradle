apply plugin: 'antlr'

sourceSets {
    antlr
}

dependencies {
    compile (
        project(':alchemist-maps'),
        "com.google.guava:guava:$guavaVersion",
        "org.danilopianini:jirf:${jirfVersion}",
        project.sourceSets.antlr.output
    )
    testCompile(
        project(':alchemist-loading'),
        project(':alchemist-engine')
    )
    antlr "org.antlr:antlr4:$antlrVersion"
    antlrCompile "org.antlr:antlr4:$antlrVersion"
}

sourcesJar {
    from sourceSets.antlr.allSource
}

generateGrammarSource {
    arguments += ["-visitor", "-package", "it.unibo.alchemist.biochemistrydsl"]
    outputDirectory = new File('src/antlr/java')
}

compileAntlrJava.dependsOn('generateGrammarSource')

checkstyle {
    sourceSets = [project.sourceSets.main, project.sourceSets.test]
}

pmd {
    sourceSets = [project.sourceSets.main, project.sourceSets.test]
}

findbugs {
    sourceSets = [project.sourceSets.main, project.sourceSets.test]
}