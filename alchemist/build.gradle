apply plugin: 'project-report'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "gradle.plugin.org.jmailen.gradle:kotlinter-gradle:$ktlintVersion"
        classpath "org.danilopianini:build-commons:${buildCommonsVersion}"
        classpath "org.danilopianini:javadoc.io-linker:${javadocIOLinkerVersion}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokkaVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

allprojects {

    scalaVersion = "${scalaMajorVersion}.${scalaMinorVersion}"

    apply plugin: 'org.danilopianini.build-commons'
    apply plugin: 'idea'
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: "org.jmailen.kotlinter"

    compileKotlin {
        kotlinOptions {
            jvmTarget = 1.8
        }
    }

    kotlinter {
        ignoreFailures = false
        indentSize = 4
        continuationIndentSize = 4
        reporters = ['checkstyle', 'plain', 'html']
    }

    /*
     * This forces the main classes to be ready before going on, preventing potential
     * issues with interleaving project tasks
     */
//    processResources.dependsOn('copyMainKotlinClasses')

    configurations {
        all*.exclude group: 'org.eclipse.xtext', module: 'org.eclipse.xtext.dependencies'
        all {
            if(!it.name.contains('antlr')) {
                resolutionStrategy {
                    forcedModules += "org.antlr:antlr-runtime:${antlrRuntimeVersion}"
                }
            }
        }
    }

    test { testLogging { events "passed", "skipped", "failed", "standardError" } }

    dependencies {
        compile "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
        compile "com.google.code.findbugs:findbugs:$findBugsVersion"
        compile "org.apache.commons:commons-math3:$math3Version"
//        compile "org.eclipse.xtend:org.eclipse.xtend.lib:${xtendLibVersion}"
        compile "org.scala-lang:scala-compiler:${scalaVersion}"
        compile "org.scala-lang:scala-library:${scalaVersion}"
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
        compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
        compile "org.danilopianini:thread-inheritable-resource-loader:${resLoaderVersion}"
        runtime "ch.qos.logback:logback-classic:$logbackVersion"

        testCompile "junit:junit:$junitVersion"

        testRuntime "ch.qos.logback:logback-classic:$logbackVersion"

        doclet "org.jboss.apiviz:apiviz:$apivizVersion"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    developers {
                        developer {
                            name 'Danilo Pianini'
                            email 'danilo.pianini@unibo.it'
                            url 'http://www.danilopianini.org'
                        }
                        developer {
                            name 'Roberto Casadei'
                            email 'roby.casadei@unibo.it'
                            url 'https://www.unibo.it/sitoweb/roby.casadei'
                        }
                    }
                    contributors {
                        contributor {
                            name 'Sara Montagna'
                            email 'sara.montagna@unibo.it'
                            url 'http://saramontagna.apice.unibo.it/'
                        }
                        contributor {
                            name 'Jacob Beal'
                            email 'jakebeal@bbn.com'
                            url 'http://web.mit.edu/jakebeal/www/'
                        }
                        contributor {
                            name 'Michele Bombardi'
                            email 'michele.bombardi@studio.unibo.it'
                            url 'http://apice.unibo.it/xwiki/bin/view/XWiki/MicheleBombardi/'
                        }
                        contributor {
                            name 'Chiara Casalboni'
                            email 'chiara.casalboni2@studio.unibo.it'
                            url 'http://apice.unibo.it/xwiki/bin/view/XWiki/ChiaraCasalboni2/'
                        }
                        contributor {
                            name 'Enrico Galassi'
                            email 'enrico.galassi@studio.unibo.it'
                            url 'http://apice.unibo.it/xwiki/bin/view/XWiki/EnricoGalassi/'
                        }
                        contributor {
                            name 'Luca Mella'
                            email 'luca.mella@studio.unibo.it'
                            url 'http://apice.unibo.it/xwiki/bin/view/XWiki/LucaMella/'
                        }
                        contributor {
                            name 'Luca Nenni'
                            email 'luca.nenni@studio.unibo.it'
                            url 'http://apice.unibo.it/xwiki/bin/view/XWiki/LucaNenni/'
                        }
                        contributor {
                            name 'Giacomo Pronti'
                            email 'giacomo.pronti@studio.unibo.it'
                            url 'http://apice.unibo.it/xwiki/bin/view/XWiki/GiacomoPronti/'
                        }
                        contributor {
                            name 'Giacomo Scaparrotti'
                            email 'giacomo.scaparrotti@studio.unibo.it'
                            url 'https://www.linkedin.com/in/giacomo-scaparrotti-0aa77569'
                        }
                        contributor {
                            name 'Gabriele Graffieti'
                            email 'gabriele.graffieti@studio.unibo.it'
                        }
                        contributor {
                            name 'Franco Pradelli'
                            email 'franco.pradelli@studio.unibo.it'
                        }
                        contributor {
                            name 'Elisa Casadio'
                            email 'elisa.casadio7@studio.unibo.it'
                        }
                        contributor {
                            name 'Matteo Francia'
                            email 'm.francia@unibo.it'
                        }
                    }
                }
            }
        }
    }
}

subprojects.each { subproject -> rootProject.evaluationDependsOn(subproject.path)}

/*
 * Remove tasks that should not exist in subprojects
 */
subprojects.each { it.tasks.remove(wrapper) }
subprojects.each { it.tasks.remove(closeRepository) }

/*
 * Running a task on the parent project implies running the same task first on any subproject
 */
tasks.each { task ->
    subprojects.each { subproject ->
        def subtask = subproject.tasks.findByPath("${task.name}")
        if (subtask != null) {
            task.dependsOn(subtask)
        }
    }
}

dependencies {
    compile subprojects
    compile (
            "commons-cli:commons-cli:$cliVersion",
    )
}

javadoc {
    dependsOn subprojects.javadoc
    source subprojects.javadoc.source
}

dokka {
    sourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
}

allprojects {
    def jdocTasks = ['javadoc', 'uploadArchives', 'projectReport', 'buildDashboard', 'javadocJar']
    def selectedTasks = gradle.startParameter.taskNames ?: defaultTasks
    if (!selectedTasks.disjoint(jdocTasks)) {
        apply plugin: 'org.danilopianini.javadoc.io-linker'
    }
}

task fatJar(type: Jar, dependsOn: subprojects.compileJava) {
    manifest {
        attributes 'Implementation-Title': 'Alchemist',
                'Implementation-Version': rootProject.version,
                'Main-Class': 'it.unibo.alchemist.Alchemist'
    }
    baseName = "${rootProject.name}-redist"
    from(configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }) {
        // remove all signature files
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude 'build'
        exclude '.gradle'
        exclude 'build.gradle'
        exclude 'gradle'
        exclude 'gradlew'
        exclude 'gradlew.bat'
    }
    with jar
}

defaultTasks 'clean', 'build', 'check', 'javadoc', 'dokka', 'assemble', 'install', 'projectReport', 'buildDashboard', 'fatJar'